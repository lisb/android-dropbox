apply plugin: 'com.android.library'
apply plugin: 'maven-publish'



android {
    compileSdkVersion android_compile_sdk_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

afterEvaluate {
    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/android-dropbox"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                groupId = 'com.dropbox.chooser.android'
                artifactId = 'chooser'
                version = '1.2.0'
            }
        }
    }
}
