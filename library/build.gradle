apply plugin: 'com.android.library'
apply plugin: 'maven-publish'



android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
}

afterEvaluate {
    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/android-dropbox"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.dropbox.chooser.android'
                artifactId = 'chooser'
                version = '1.0.3-SNAPSHOT'
            }
        }
    }
}
